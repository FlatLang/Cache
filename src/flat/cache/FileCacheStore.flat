package flat/cache

import flat/io
import flat/datastruct
import flat/time

class<TKey, TValue> implements CacheStore<TKey, TValue> {
    HashMap<TKey, File> files = HashMap()

    Array<func(TValue value) -> String> serializer = Array()
    Array<func(String str) -> TValue> deserializer = Array()
    Array<func(TKey key) -> String> keyToFileMapper = Array()

    public construct(
        serializer(TValue) -> String,
        deserializer(String) -> TValue,
        keyToFileMapper(TKey) -> String
    ) {
        this.serializer.add(serializer)
        this.deserializer.add(deserializer)
        this.keyToFileMapper.add(keyToFileMapper)
    }

    override public async invalidate(TKey key) {
        if (File file = files[key]) {
            if (!file.exists) return

            file.delete()
        }
    }

    override public async contains(TKey key) -> Bool {
        return files.containsKey(key)
    }

    override public async get(TKey key) -> TValue => null {
        if (File file = files[key]) {
            let contents = FileReader(file).readAllContents()
            let data = contents.substring(contents.indexOf(':') + 1)
            return deserializer.map({ _(data) }).first
        }
    }

    override public async store(TKey key, TValue value) -> TValue {
        File file = files.getOrDefault(key, { keyToFileMapper.map({ _(key) }).first })

        if (!file.getParent().exists) {
            file.getParent().mkdirs()
        }

        let data = serializer.map({ _(value) }).first
        let timestamp = Time.currentTimeMillis

        FileWriter(file)
            .write("#{timestamp}:#{data}")
            .close()

        return value
    }

    override public async checkExpired(TKey key, Long lifetime) => true {
        if (File file = files[key]) {
            let contents = FileReader(file).readAllContents()
            let data = contents.substring(end: contents.indexOf(':'))
            let timestamp = Long.parseLong(data)
            return Time.currentTimeMillis - timestamp >= lifetime
        }
    }
}
